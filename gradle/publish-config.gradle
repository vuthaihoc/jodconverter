apply plugin: 'signing'
apply plugin: 'maven-publish'

ext.isSnapshotVersion = version.endsWith('SNAPSHOT')
ext.sonatypeRepo = isSnapshotVersion
        ? 'https://oss.sonatype.org/content/repositories/snapshots/'
        : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

signing {
    // TODO: Check this issue from time to time to use the commented instructions instead.
    // https://github.com/gradle/gradle/issues/11387
    //required { !isSnapshotVersion && findProperty('ossrhUsername') != null && findProperty('ossrhPassword') != null }
    //sign publishing.publications
    required { !isSnapshotVersion && hasProperty('ossrhUsername') && hasProperty('ossrhPassword') }
    if (required) {
        //sign configurations.archives
        sign publishing.publications
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            customizePom(pom, project)
        }
    }
    repositories {
        maven {
            url sonatypeRepo
            credentials {
                username project.findProperty("ossrhUsername")
                password project.findProperty("ossrhPassword")
            }
        }
    }
}

def customizePom(pom, p) {
    pom.withXml {
        def root = asNode()

        // Add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name p.hasProperty('moduleName') ? p.moduleName : p.name
            description p.hasProperty('moduleDescription') ? p.moduleDescription : p.description
            url 'http://jodconverter.org'
            organization {
                name 'JODConverter'
                url 'http://jodconverter.org'
            }
            scm {
                url 'https://github.com/sbraconnier/jodconverter.git'
                connection 'scm:git:https://github.com/sbraconnier/jodconverter.git'
                developerConnection 'scm:git:git@https://github.com/sbraconnier/jodconverter.git'
            }
            developers {
                developer {
                    id 'sbraconnier'
                    name 'Simon Braconnier'
                    email 'simonbraconnier@gmail.com'
                    timezone  '-5'
                }
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/sbraconnier/jodconverter/issues'
            }
            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }
}
